ENTRY(Start)

gNumMusicPlayers = 4;
gMaxLines = 0;

MEMORY {
	ewram_region    (w!x) : ORIGIN = 0x02000000, LENGTH =   0x40000
	iwram_region    (w!x) : ORIGIN = 0x03000000, LENGTH =    0x8000
	rom_region      (rx)  : ORIGIN = 0x08000000, LENGTH = 0x1000000
}

SECTIONS {
    ewram (NOLOAD) :
    ALIGN(4)
    {
        gHeap = .;

        . = 0x1C000;

        src/*.o(ewram_data);
	} >ewram_region

    iwram (NOLOAD) :
    ALIGN(4)
    {
        /* .bss starts at 0x3000000 */
        src/*.o(.bss);
		asm/m4a_1.o(.bss);

        /* .bss.code starts at 0x3001AA8 */
        src/m4a.o(.bss.code);

        /* COMMON starts at 0x30022A8 */
        src/*.o(COMMON);
        *libc.a:sbrkr.o(COMMON);
        end = .;
    } >iwram_region

    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        src/*.o(.text);
        asm/*.o(.text);
    } >rom_region =0

    script_data :
    ALIGN(4)
    {
		data/*.o(script_data);
    } >rom_region =0

    lib_text :
    ALIGN(4)
    {
        asm/libgcnmultiboot.o(.text);
        asm/m4a_1.o(.text);
        src/m4a.o(.text);
        src/agb_flash.o(.text);
        src/agb_flash_1m.o(.text);
        src/agb_flash_mx.o(.text);
        src/siirtc.o(.text);
        src/librfu_stwi.o(.text);
        src/librfu_intr.o(.text);
        asm/librfu_intr.o(.text);
        src/librfu_rfu.o(.text);
        asm/librfu.o(.text);
        asm/libagbsyscall.o(.text);
        *libgcc.a:*.o(.text*);
        *libc.a:*.o(.text*);
        src/libisagbprn.o(.text);
    } >rom_region =0

    .rodata :
    ALIGN(4)
    {
        src/*.o(.rodata*);
        data/*.o(.rodata*);
    } >rom_region =0

    song_data :
    ALIGN(4)
    {
        sound/songs/*.o(.rodata);
    } >rom_region =0

    lib_rodata :
    SUBALIGN(4)
    {
        src/m4a.o(.rodata);
        src/agb_flash.o(.rodata);
        src/agb_flash_1m.o(.rodata);
        src/agb_flash_mx.o(.rodata);
        src/agb_flash_le.o(.rodata);
        src/siirtc.o(.rodata);
        data/librfu_rodata.o(.rodata);
        *libgcc.a:*.o(.rodata*);
        *libc.a:*.o(.rodata*);
        *libc.a:*.o(.data*);
        src/libisagbprn.o(.rodata);
    } >rom_region =0

    other_data :
    ALIGN(4)
    {
        data/unknown_serial_data.o(.rodata);
        data/multiboot_berry_glitch_fix.o(.rodata);
        data/multiboot_pokemon_colosseum.o(.rodata);
    } >rom_region =0

    anim_mon_front_pic_data :
    ALIGN(4)
    {
        src/anim_mon_front_pics.o(.rodata);
    } >rom_region =0

    gfx_data :
    ALIGN(4)
    {
        src/graphics.o(.rodata);
    } >rom_region =0

    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
