ENTRY(Start)

gNumMusicPlayers = 4;
gMaxLines = 0;

SECTIONS {
    . = 0x2000000;

    ewram (NOLOAD) :
    ALIGN(4)
    {
        gHeap = .;

        . = 0x1C000;

        src/*.o(ewram_data);
        gflib/*.o(ewram_data);

        . = 0x40000;
}

    . = 0x3000000;

    iwram (NOLOAD) :
    ALIGN(4)
    {
        /* .bss starts at 0x3000000 */
        src/*.o(.bss);
        gflib/*.o(.bss);
        *libc.a:*.o(.bss*);
        *libnosys.a:*.o(.bss*);

        /* .bss.code starts at 0x3001AA8 */
        src/m4a.o(.bss.code);

        /* COMMON starts at 0x30022A8 */
        src/*.o(COMMON);
        gflib/*.o(COMMON);
        *libc.a:*.o(COMMON);
        *libnosys.a:*.o(COMMON);
        end = .;
        . = 0x8000;
    }

    . = 0x8000000;

    .text :
    ALIGN(4)
    {
        src/crt0.o(.text*);
        src/*.o(.text*);
        gflib/*.o(.text*);
        asm/*.o(.text*);
    } =0

    script_data :
    ALIGN(4)
    {
		data/*.o(script_data);
    } =0

    lib_text :
    ALIGN(4)
    {
        src/libgcnmultiboot.o(.text);
        src/m4a_1.o(.text);
        src/m4a.o(.text);
        src/agb_flash.o(.text);
        src/agb_flash_1m.o(.text);
        src/agb_flash_mx.o(.text);
        src/siirtc.o(.text);
        src/librfu_stwi.o(.text);
        src/librfu_intr.o(.text);
        src/librfu_rfu.o(.text);
        src/librfu_sio32id.o(.text);
        *libagbsyscall.a:*.o(.text*);
        *libgcc.a:*.o(.text*);
        *libc.a:*.o(.text*);
        *libnosys.a:*.o(.text*);
        src/libisagbprn.o(.text);
    } =0

    .rodata :
    ALIGN(4)
    {
        src/*.o(.rodata*);
        gflib/*.o(.rodata*);
        data/*.o(.rodata*);
    } =0

    song_data :
    ALIGN(4)
    {
        sound/songs/*.o(.rodata);
    } =0

    lib_rodata :
    SUBALIGN(4)
    {
        src/m4a.o(.rodata);
        src/agb_flash.o(.rodata);
        src/agb_flash_1m.o(.rodata);
        src/agb_flash_mx.o(.rodata);
        src/agb_flash_le.o(.rodata);
        src/siirtc.o(.rodata);
        src/librfu_rfu.o(.rodata);
        src/librfu_sio32id.o(.rodata);
        *libgcc.a:*.o(.rodata*);
        *libc.a:*.o(.rodata*);
        *libc.a:*.o(.data*);
        src/libisagbprn.o(.rodata);
    } =0

    other_data :
    ALIGN(4)
    {
        data/unknown_serial_data.o(.rodata);
        data/multiboot_berry_glitch_fix.o(.rodata);
        data/multiboot_pokemon_colosseum.o(.rodata);
    } =0

    anim_mon_front_pic_data :
    ALIGN(4)
    {
        src/anim_mon_front_pics.o(.rodata);
    } =0

    gfx_data :
    ALIGN(4)
    {
        src/graphics.o(.rodata);
    } =0


    doom :
    ALIGN(4)
    {
        src/GBADoom/source/am_map.o;
        src/GBADoom/source/fixeddiv.o;
        src/GBADoom/source/d_client.o;
        src/GBADoom/source/d_items.o;
        src/GBADoom/source/d_main.o;
        src/GBADoom/source/doom_iwad.o;
        src/GBADoom/source/f_finale.o;
        src/GBADoom/source/f_wipe.o;
        src/GBADoom/source/g_game.o;
        src/GBADoom/source/global_data.o;
        src/GBADoom/source/hu_lib.o;
        src/GBADoom/source/hu_stuff.o;
        src/GBADoom/source/i_audio.o;
        src/GBADoom/source/i_main.o;
        src/GBADoom/source/i_system.o;
        src/GBADoom/source/i_system_e32.o;
        src/GBADoom/source/i_system_gba.o;
        src/GBADoom/source/i_video.o;
        src/GBADoom/source/info.o;
        src/GBADoom/source/lprintf.o;
        src/GBADoom/source/m_bbox.o;
        src/GBADoom/source/m_cheat.o;
        src/GBADoom/source/m_menu.o;
        src/GBADoom/source/m_random.o;
        src/GBADoom/source/m_recip.o;
        src/GBADoom/source/p_ceilng.o;
        src/GBADoom/source/p_doors.o;
        src/GBADoom/source/p_enemy.o;
        src/GBADoom/source/p_floor.o;
        src/GBADoom/source/p_genlin.o;
        src/GBADoom/source/p_inter.o;
        src/GBADoom/source/p_lights.o;
        src/GBADoom/source/p_map.o;
        src/GBADoom/source/p_maputl.o;
        src/GBADoom/source/p_mobj.o;
        src/GBADoom/source/p_plats.o;
        src/GBADoom/source/p_pspr.o;
        src/GBADoom/source/p_setup.o;
        src/GBADoom/source/p_sight.o;
        src/GBADoom/source/p_spec.o;
        src/GBADoom/source/p_switch.o;
        src/GBADoom/source/p_telept.o;
        src/GBADoom/source/p_tick.o;
        src/GBADoom/source/p_user.o;
        src/GBADoom/source/r_data.o;
        src/GBADoom/source/r_draw.o;
        src/GBADoom/source/r_hotpath.iwram.o;
        src/GBADoom/source/r_main.o;
        src/GBADoom/source/r_patch.o;
        src/GBADoom/source/r_plane.o;
        src/GBADoom/source/r_things.o;
        src/GBADoom/source/s_sound.o;
        src/GBADoom/source/sounds.o;
        src/GBADoom/source/st_gfx.o;
        src/GBADoom/source/st_lib.o;
        src/GBADoom/source/st_stuff.o;
        src/GBADoom/source/tables.o;
        src/GBADoom/source/v_video.o;
        src/GBADoom/source/version.o;
        src/GBADoom/source/w_wad.o;
        src/GBADoom/source/wi_stuff.o;
        src/GBADoom/source/z_bmalloc.o;
        src/GBADoom/source/z_zone.o
    } =0

    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
